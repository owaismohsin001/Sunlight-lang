external "std/modify" {baseModify}
external "std/intify" {stringToInt, isNumBase}
external "std/exponent" {unsafeExponent}

id: a <- a

const: a <- (\_ -> a)

min: a, b <- if a > b then b else a
max: a, b <- if a > b then a else b

even <- \x {%} 2 = 0
odd <- \x {%} 2 /= 0

cmp: f, g <- \f: g: x
lcmp <- flip: cmp

fst: t <- a where (a, _) <- t end
snd: t <- b where (_, b) <- t end
swap: t <- (b, a) where (a, b) <- t end

flip: f, x, y <- f: y, x

add: a, b <- a+b
sub: a, b <- a-b
mul: a, b <- a*b
div: a, b <- a/b

subFlip <- flip: sub
divFlip <- flip: div

{|-} <- subFlip
{|/} <- divFlip

decN: n <- (-n)add

appendList: a, b <- a .. b

inc <- 1add
dec <- 1decN

double <- 2mul
half <- \if x = 0 then 0 else x/2

cons <- ~\[x]appendList

{::} <- cons

class safeDiv: a, b
b = 0 -> none
true -> some: a/b

flipSafeDiv <- flip: safeDiv

class modulus: n
n@SltNum -> unsafeMod: n

class safeModulus: a, b
b = 0 -> none
true -> some: a modulus b

open isNum: s
isNum ? s@SltString -> isNumBase: s

not <- \if x then false else true
notB <- not cmp bool

and: a, b <- a & b
or: a, b <- a | b

andB: a, b <- (bool: a) and (bool: b)
orB: a, b <- (bool: a) or (bool: b)

open stringify: v
stringify ? def -> baseStringify: v

open bool: s
bool ? s@SltNum -> s /= 0
bool ? s@SltString -> s /= ""
bool ? s@SltBool -> s

open numify: s
numify ? s@SltString -> stringToInt: s
numify ? s@SltBool -> if s then 1 else 0
numify ? s@SltNum -> s

class expo: a, b
a@SltNum & b@SltNum -> unsafeExponent: a, b

{**} <- expo
{%} <- modulus
{#} <- numify
{$} <- stringify

class modify: st, el, f
el@SltString & f@SltFunc -> baseModify: st, el, f

{.} <- cmp
{|.} <- lcmp